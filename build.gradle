buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "kotlinx-serialization"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "maven-publish"

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

def currentVersion = new File("version.txt").text.trim()

mainClassName = "io.ktor.server.tomcat.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ["src"]
    test.kotlin.srcDirs = test.java.srcDirs = ["test"]
    main.resources.srcDirs = ["resources"]
    test.resources.srcDirs = ["testresources"]
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"

    implementation "io.ktor:ktor-server-tomcat:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-serialization:$ktor_version"
    implementation "io.ktor:ktor-websockets:$ktor_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"

    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jodatime:$exposed_version"

    implementation "org.apache.kafka:kafka-clients:$kafka_version"
    implementation "redis.clients:jedis:$jedis_version"

    implementation "org.postgresql:postgresql:$postgresql_version"
    implementation "com.zaxxer:HikariCP:$hikari_version"
    implementation "com.google.cloud.sql:postgres-socket-factory:$postgresql_cloud"

    implementation "org.kodein.di:kodein-di-generic-jvm:$kodein_version"

    implementation "com.google.cloud:google-cloud-storage:$cloud_storage_verion"

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "io.mockk:mockk:$mockk_version"
}

shadowJar {
    baseName = "public-poll-server"
    classifier = null
    version = currentVersion
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            group = "com.dvpermyakov"
            version = currentVersion
            artifactId = "public-poll-server"
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            url System.getenv("ARTIFACT_REGISTRY_URL")
            credentials {
                username = "_json_key_base64"
                password = System.getenv("ARTIFACT_REGISTRY_PASSWORD")
            }
        }
    }
}